' SPDX-License-Identifier: EUPL-1.2
' This file is part of Ecopath with Ecosim (EwE).
' Copyright © 1991– Ecopath International Initiative (EII)

Imports System.Drawing
Imports System.IO
Imports System.Text
Imports System.Xml
Imports EwECore
Imports EwECore.Common
Imports EwEUtils.Utilities

Namespace SpatialData

    ''' -----------------------------------------------------------------------
    ''' <summary>
    ''' <see cref="ISpatialDataSet"/> for accessing a single spatial file without
    ''' any temporal attribute.
    ''' </summary>
    ''' -----------------------------------------------------------------------
    Public Class cSingleFileDataSetPlugin
        Inherits cFileDataSetPlugin

#Region " Private vars "

        Private m_indexstatus As ISpatialDataSet.eIndexStatus = ISpatialDataSet.eIndexStatus.NotIndexed
        Private m_ptTL As New PointF(0, 0)
        Private m_ptBR As New PointF(0, 0)
        Private m_datatime As Date = Date.MinValue

#End Region ' Private vars

#Region " Construction / destruction "

        ''' -------------------------------------------------------------------
        ''' <summary>
        ''' Constructor.
        ''' </summary>
        ''' -------------------------------------------------------------------
        Public Sub New()
            MyBase.New()
            Me.VarName = eVarNameFlags.NotSet
        End Sub

        Public Sub New(ds As cSingleFileDataSetPlugin)
            MyBase.New(ds)
            Me.Source = ds.Source
            Me.FileDate = ds.FileDate
        End Sub

#End Region ' Construction / destruction

#Region " Information "

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="cFileDataSetPlugin.CustomName" />
        ''' -------------------------------------------------------------------
        Public Overrides Property CustomName As String
            Get
                If (Not String.IsNullOrWhiteSpace(Me.Name)) Then
                    Return Me.Name
                End If
                If (Not String.IsNullOrWhiteSpace(Me.Source)) Then
                    Return cStringUtils.Localize(My.Resources.DATASET_SINGLE_DISPLAYNAME, Path.GetFileName(Me.Source))
                End If
                Return My.Resources.DATASET_SINGLE_NAME
            End Get
            Set(value As String)
                Me.Name = value
            End Set
        End Property

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="cFileDataSetPlugin.DateStart"/>
        ''' -------------------------------------------------------------------
        Public Overrides ReadOnly Property DateStart As DateTime
            Get
                Return Me.FileDate
            End Get
        End Property

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="cFileDataSetPlugin.DateEnd"/>
        ''' -------------------------------------------------------------------
        Public Overrides ReadOnly Property DateEnd As DateTime
            Get
                Return If(Me.FileDate = Date.MinValue, Date.MaxValue, Me.FileDate)
            End Get
        End Property

        ''' -------------------------------------------------------------------
        ''' <summary>
        ''' Date that this single data point adheres to.
        ''' </summary>
        ''' -------------------------------------------------------------------
        Public Property FileDate As DateTime
            Get
                Return Me.m_datatime
            End Get
            Set(value As DateTime)
                Me.m_datatime = value
                Me.m_indexstatus = ISpatialDataSet.eIndexStatus.NotIndexed
            End Set
        End Property

        ''' -------------------------------------------------------------------
        ''' <summary>
        ''' Returns whether the dataset equals another.
        ''' </summary>
        ''' -------------------------------------------------------------------
        Public Overrides Function Equals(obj As Object) As Boolean
            If (obj Is Nothing) Then Return False
            If (Not TypeOf obj Is cSingleFileDataSetPlugin) Then Return False

            Dim sfd As cSingleFileDataSetPlugin = DirectCast(obj, cSingleFileDataSetPlugin)

            Return (String.Compare(Me.SourceFileName, sfd.SourceFileName, True) = 0) And
                   (Me.DateStart = sfd.DateStart) And
                   (Me.DateEnd = sfd.DateEnd)
        End Function

#End Region ' Information

#Region " Configuration "

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="cFileDataSetPlugin.GetConfigUI"/>
        ''' -------------------------------------------------------------------
        Public Overrides Function GetConfigUI() As Object
            Return Nothing
        End Function

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="cFileDataSetPlugin.IsConfigured"/>
        ''' -------------------------------------------------------------------
        Public Overrides Function IsConfigured() As Boolean
            Return Not String.IsNullOrWhiteSpace(Me.Source)
        End Function

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="cFileDataSetPlugin.IsDataAvailable"/>
        ''' -------------------------------------------------------------------
        Public Overrides Function IsDataAvailable(ByVal runtype As IRunType) As Boolean
            Return Me.IsConfigured And Me.EnableData(runtype)
        End Function

        ''' -------------------------------------------------------------------
        ''' <summary>
        ''' Write content to XML.
        ''' </summary>
        ''' <param name="doc">The doc to generate nodes for.</param>
        ''' <returns>
        ''' An XML node that contains the content of the dataset.
        ''' </returns>
        ''' -------------------------------------------------------------------
        Protected Overrides Function ToXML(ByVal doc As XmlDocument,
                                           ByVal strFolderRoot As String) As XmlNode

            Dim xnMaster As XmlNode = Nothing
            Dim xn As XmlNode = Nothing
            Dim xnFile As XmlNode = Nothing
            Dim xaFile As XmlAttribute = Nothing
            Dim cin As cCoreEnumNamesIndex = cCoreEnumNamesIndex.GetInstance()

            xnMaster = doc.CreateElement("Configuration")

            xn = doc.CreateElement("Name")
            xn.InnerText = Me.Name
            xnMaster.AppendChild(xn)

            xn = doc.CreateElement("Description")
            xn.InnerText = Me.CustomDescription
            xnMaster.AppendChild(xn)

            xn = doc.CreateElement("Variable")
            xn.InnerText = cin.GetVarName(Me.VarName)
            xnMaster.AppendChild(xn)

            xnFile = doc.CreateElement("File")
            If (Me.IsSourceRelative) Then
                xnFile.InnerText = Me.ToRelativePath(Me.Source, strFolderRoot)
            Else
                xnFile.InnerText = Me.Source
            End If

            xaFile = doc.CreateAttribute("IsRelative")
            xaFile.Value = Convert.ToString(Me.IsSourceRelative)
            xnFile.Attributes.Append(xaFile)

            xaFile = doc.CreateAttribute("Date")
            xaFile.Value = cStringUtils.FormatDate(Me.FileDate, "d")
            xnFile.Attributes.Append(xaFile)

            xnMaster.AppendChild(xnFile)

            Return xnMaster

        End Function

        ''' -------------------------------------------------------------------
        ''' <summary>
        ''' Read content from XML.
        ''' </summary>
        ''' <param name="doc">The doc to read nodes from.</param>
        ''' <param name="node">The configuration node that contains the content
        ''' of the dataset. Happy, happy, happy.</param>
        ''' <returns>
        ''' True if successful.
        ''' </returns>
        ''' -------------------------------------------------------------------
        Protected Overrides Function FromXML(ByVal doc As XmlDocument,
                                             ByVal node As XmlNode,
                                             ByVal strFolderRoot As String) As Boolean

            Dim xn As XmlNode = Nothing
            Dim cin As cCoreEnumNamesIndex = cCoreEnumNamesIndex.GetInstance()

            If (String.Compare(node.Name, "Configuration") <> 0) Then Return False

            Try

                For Each xn In node.ChildNodes
                    Select Case xn.Name
                        Case "Name"
                            Me.Name = xn.InnerText

                        Case "Description"
                            Me.CustomDescription = xn.InnerText

                        Case "Variable"
                            Me.VarName = cin.GetVarName(xn.InnerText)
                            ' JS 25Nov15 (Happy first birthday Lara) removed dangerous backward compatibility
                            'If (Me.VarName = eVarNameFlags.NotSet And cStringUtils.IsNumber(xn.InnerText)) Then
                            '    Me.VarName = DirectCast(CInt(xn.InnerText), eVarNameFlags)
                            'End If

                        Case "File"
                            ' -- Source --
                            ' Backwards compatibility
                            If (xn.Attributes.GetNamedItem("Source") IsNot Nothing) Then
                                Me.Source = xn.Attributes("Source").InnerText
                            Else
                                Me.Source = xn.InnerText
                            End If

                            Me.IsSourceRelative = False
                            If (xn.Attributes.GetNamedItem("IsRelative") IsNot Nothing) Then
                                Me.IsSourceRelative = Boolean.Parse(xn.Attributes("IsRelative").InnerText)
                            End If
                            ' Backwards compatibility
                            If (xn.Attributes.GetNamedItem("IsSourceRelative") IsNot Nothing) Then
                                Me.IsSourceRelative = Boolean.Parse(xn.Attributes("IsSourceRelative").InnerText)
                            End If

                            ' -- Date --
                            Dim strDate As String = ""
                            Dim dt As DateTime = Nothing
                            ' Backwards compatibility
                            If (xn.Attributes.GetNamedItem("DateRef") IsNot Nothing) Then
                                strDate = xn.Attributes("DateRef").InnerText
                                dt = cStringUtils.ConvertToDate(strDate, "dd/MM/yyyy")
                            Else
                                strDate = xn.Attributes("Date").InnerText
                                dt = cStringUtils.ConvertToDate(strDate)
                            End If
                            Me.FileDate = dt

                            ' -- Index status --
                            Me.m_indexstatus = ISpatialDataSet.eIndexStatus.NotIndexed

                    End Select
                Next

            Catch ex As Exception
                Return False
            End Try

            ' Resolve relative source path to the current config file location
            If (Me.IsSourceRelative) Then
                Me.Source = Me.ToAbsolutePath(Me.Source, strFolderRoot)
            End If

            Return True

        End Function

#End Region ' Configuration

#Region " Data "

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="ISpatialDataSet.HasDataAtT"/>
        ''' -------------------------------------------------------------------
        Public Overrides Function HasDataAtT(ByVal time As DateTime) As Boolean
            If (Me.m_core Is Nothing) Then Return True
            Dim t1 As Integer = Me.m_core.AbsoluteTimeToEcospaceTimestep(time)
            If Me.m_datatime = Date.MinValue Then Return (t1 = 1)
            Dim t2 As Integer = Me.m_core.AbsoluteTimeToEcospaceTimestep(Me.m_datatime)
            Return t1 = t2
        End Function

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="ISpatialDataSet.GetExtentAtT"/>
        ''' -------------------------------------------------------------------
        Public Overrides Function GetExtentAtT(ByVal datetime As Date,
                                               ByRef ptfTL As System.Drawing.PointF,
                                               ByRef ptfBR As System.Drawing.PointF) As Boolean

            ' Return cached extent
            ptfTL = Me.m_ptTL
            ptfBR = Me.m_ptBR
            Return (ptfTL <> ptfBR)

        End Function

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="cFileDataSetPlugin.IndexStatusAtT"/>
        ''' -------------------------------------------------------------------
        Protected Overrides Function IndexStatusAtT(dt As Date) As ISpatialDataSet.eIndexStatus
            Return Me.m_indexstatus
        End Function

        ''' -------------------------------------------------------------------
        ''' <inheritdocs cref="cFileDataSetPlugin.UpdateIndexAtT"/>
        ''' -------------------------------------------------------------------
        Protected Overrides Sub UpdateIndexAtT(ByVal dateStart As DateTime)

            Dim ptfTL As New PointF(-180, 90)
            Dim ptfBR As New PointF(180, -90)
            Dim c As ISpatialDataCache = Me.Cache

            If (Me.m_indexstatus <> ISpatialDataSet.eIndexStatus.Indexed) And (Me.IsConfigured) Then
                Try
                    Me.Cache = Nothing
                    If Me.LockDataAtT(Nothing, 1.0!, ptfTL, ptfBR, "") Then
                        Me.LoadSource()
                        Me.UnlockData()
                    End If
                Catch ex As Threading.ThreadAbortException
                    ' OK
                Catch ex As Exception
                    ' Not ok
                Finally
                    Me.UnlockData()
                    Me.Cache = c
                End Try
            End If

        End Sub

#End Region ' Data

#Region " Internals "

        Protected Overrides Function CacheFileName(ByVal strLayerName As String) As String
            Return cFileUtils.MakeTempFile("stdf")
        End Function

        Protected Overrides Function SourceFileName() As String
            Return Me.Source
        End Function

#End Region ' Internals

#Region " Plug-in implementation "

        ''' -----------------------------------------------------------------------
        ''' <inheritdocs cref=IPlugin.Description"/>
        ''' -----------------------------------------------------------------------
        Public Overrides ReadOnly Property Description As String
            Get
                Return "Plug-in that provides access to a dataset that contains a single time-stamped spatial file"
            End Get
        End Property

        ''' -----------------------------------------------------------------------
        ''' <inheritdocs cref=IPlugin.Name"/>
        ''' -----------------------------------------------------------------------
        Public Overrides ReadOnly Property PluginName As String
            Get
                Return "STDF.Dataset.SingleFile"
            End Get
        End Property

        ''' -----------------------------------------------------------------------
        ''' <inheritdocs cref=IPlugin.DisplayName"/>
        ''' -----------------------------------------------------------------------
        Public Overrides ReadOnly Property PluginDisplayName As String
            Get
                Return "Single file dataset"
            End Get
        End Property

#End Region ' Plug-in implementation

#Region " Import & export "

        Public Overrides Function ExportTo(ByVal strPath As String) As ISpatialDataSet

            ' Sanity checks
            Debug.Assert(Not Convert.Equals(Guid.Empty, Me.DBID), "Dataset has no valid ID yet")

            ' Clone DS
            Dim ds As cSingleFileDataSetPlugin = New cSingleFileDataSetPlugin(Me)

            ' Export file content to a folder in strPath that is identified by the current GUID
            ' Note that the exported dataset will inherit the same GUID. It makes sense but may cause confusion...
            Dim strFolder As String = cFileUtils.ToValidFileName(Me.CustomName, False)
            Dim strAbsPath As String = Path.Combine(strPath, strFolder)
            Dim strAbsFile As String = Path.Combine(strAbsPath, Path.GetFileName(Me.Source))

            ' Internally, source is ALWAYS absolute
            ds.IsSourceRelative = True
            ds.Source = strAbsFile

            ' Make sure that the path exists
            If Not cFileUtils.IsDirectoryAvailable(strAbsPath, True) Then
                ' ToDo: send some kind of message
                Return Nothing
            End If

            ' Copy file
            Try
                File.Copy(Me.Source, ds.Source, True)
            Catch ex As Exception
                ' ToDo: send some kind of message
                Return Nothing
            End Try

            ' Clear index status on the new dataset; file presence must be re-assessed wherever the dataset is used
            ds.m_indexstatus = ISpatialDataSet.eIndexStatus.NotIndexed

            ' Return clone
            Return ds

        End Function

#End Region ' Import & export

#Region " Summary "

        Public Overrides ReadOnly Property Summary As String
            Get
                Dim sb As New StringBuilder()
                sb.Append("id:" & Me.GetType().ToString())
                sb.Append(",")
                sb.Append("ts:" & cStringUtils.FormatDate(Me.DateStart))
                sb.Append(",")
                sb.Append("te:" & cStringUtils.FormatDate(Me.DateEnd))
                sb.Append(",")
                sb.Append("f:")
                If IO.File.Exists(Me.SourceFileName) Then
                    sb.Append(Path.GetFileName(Me.SourceFileName))
                Else
                    sb.Append("?")
                End If
                Return sb.ToString()
            End Get
        End Property

#End Region ' Summary

    End Class

End Namespace
